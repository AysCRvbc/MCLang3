@name nextbot

@define expression modules.particle_generator
@define block modules.mod_block
@define expression modules.spawn_generator
@define block modules.rule_block
@define expression modules.nbt_expression
@define block modules.float_block
@define expression modules.pass_expression
@define expression modules.tellraw_generator


nbt rot_axis0 = Rotation[0]
nbt rot_axis1 = Rotation[1]
nbt target_health = Health
float zero
float axis0
float axis1
float health

selector stands = @e[type=armor_stand]

image lmao1 = "./mcls/assets/rock.png"
image lmao2 = "./mcls/assets/rock2.png"
image lmao3 = "./mcls/assets/rock3.jpg"

tellrope menu = "./mcls/assets/nextbot_menu.html"

var difficulty

chat rock = ["<Скала> "]
chat system = ["NEXTBOTMOD: "]

func pargen_activate() -> stands
    mod at @s
        execute data modify entity @s NoGravity set value 1b
        execute data modify entity @s Invisible set value 1b
        execute data merge entity @s {CustomName:'{"text":"Страшный мужик"}'}
        execute tp ~10 ~10 ~10
        zero.set(0)
        self.pargen = True

input activate(num) -> @a
    mod at @s
        spawn minecraft:armor_stand pargen_activate

input clear(num) -> @a
    mod as @e[type=armor_stand]
        execute kill @s

input menu(num) -> @a
    menu.show(@s)

input getDifficulty(num) -> @a
    difficulty.store(difficulty)
    system.log(@s, ["Значение сложности -> ", difficulty])


rule pgen(pargen) -> stands
    var prevBlockUnder
    mod at @s
        # Расчет угла поворота к игроку
        foundPlayer = False
        mod facing entity @p[distance=..20] eyes
            foundPlayer = True
            execute tp ^ ^ ^0.1
            axis0.setFromNbt(rot_axis0)
            axis1.setFromNbt(rot_axis1)
            execute tp ^ ^ ^-0.1

        if foundPlayer == False
            axis0.sub(1)
            rot_axis0.storeFloat(axis0)
            rot_axis1.storeFloat(zero)

        canMove = False
        mod if block ~ ~ ~ minecraft:air
            canMove = True

        isBlockUp = True
        mod if block ~ ~1 ~ minecraft:air
            isBlockUp = False

        if canMove == False
            execute tp ~ ~0.2 ~

        isBlockUnder = True
        mod if block ~ ~-1 ~ minecraft:air
            isBlockUnder = False

        if isBlockUnder == False
            canMove = False
            execute tp ^ ^-1 ^

        if prevBlockUnder == isBlockUnder
            pass
        else
            execute playsound minecraft:item.shovel.flatten player @p[distance=..20]
            execute particle minecraft:explosion_emitter ~ ~ ~ 0 0 0 1 1 force @a

        # Логика поведения
        sprite = 2
        dealDamage = False
        mod if entity @p[distance=..20]
            health.setFromNbt(target_health, @p)
            mod if entity @p[distance=..2]
                dealDamage = True
            if health < 200
                dealDamage = False

            if canMove
                rot_axis0.storeFloat(axis0)
                rot_axis1.storeFloat(axis1)
                execute tp ^ ^ ^0.3

            player5blocks = 0
            mod if entity @p[distance=..5]
                player5blocks = 1

            if player5blocks == 0
                sprite = 1
            else
                sprite = 3

        if sprite == 1
            lmao1.show(2, 2, 0.5, 0.05)
        if sprite == 2
            lmao2.show(2, 2, 0.5, 0.04)
        if sprite == 3
            lmao3.show(2, 2, 0.5, 0.04)

        if dealDamage == True
            mod as @p
                execute damage @s 3 minecraft:indirect_magic

        prevBlockUnder = isBlockUnder
